
import static groovy.io.FileType.*

buildscript {
  repositories { 
    mavenCentral() 
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  }
  dependencies {
    classpath 'com.aestasit.infrastructure.sshoogr:sshoogr-gradle:0.9.20-SNAPSHOT'
  }
}

apply plugin: 'secureShell'

sshOptions {
  defaultUser           = 'pi'
  defaultPassword       = 'raspberry'
  reuseConnection       = true
  trustUnknownHosts     = true
  verbose               = true
  execOptions {
    verbose             = true
    showOutput          = true
    showCommand         = true
    succeedOnExitStatus = 0
  }
  scpOptions {
    showProgress        = false
  }
  sshDebug              = true
}

project.ext.remote = { Closure cl ->
  file('hosts').eachFileMatch(FILES, ~/^.*?\.groovy$/) { File hostFile ->
    if (!project.hasProperty('host') || project.host.split(',').contains(hostFile.name.replace('.groovy', ''))) {
      project.ext.hostConfig = new ConfigSlurper().parse(hostFile.text)
      remoteSession("${hostConfig.user}:${hostConfig.password ?: latcraftIOTDevicePassword}@${hostConfig.host}:22", cl)
    }
  }
}

apply from: 'gradle/setup.gradle'
apply from: 'gradle/ops.gradle'
apply from: 'gradle/temp.gradle'


